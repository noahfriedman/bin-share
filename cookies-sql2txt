#!/usr/bin/env perl
# cookies-sql2txt --- generate cookies.txt from sqlite index
# Author: Noah Friedman <friedman@splode.com>
# Created: 2009-02-10
# Public domain

# $Id$

# Commentary:

# Firefox 3.0 and later use a SQLite database for storing cookies.
# Other web fetching tools such as wget still use the traditional Netscape
# cookies.txt file.  This program will generate the latter from the former.

# Code:

$^W = 1; # enable warnings

use DBI;
use strict;

sub main
{
  my ($cookiedb) = @_;
  unless (defined $cookiedb)
    {
      (my $prog = $0) =~ s=.*/==;
      print STDERR "Usage: $prog [cookies.sqlite]\n";
      return 1;
    }

  my $dbh = DBI->connect ("DBI:SQLite:dbname=$cookiedb");
  return 1 unless $dbh;

  my $stm = "select host,
                    replace(replace(substr(host,0,1)='.',1,'TRUE'),0,'FALSE') as flag,
                    path,
                    isSecure,
                    expiry,
                    name,
                    value
             from moz_cookies
             where expiry >= ?
            ";
  my $sth = $dbh->prepare ($stm);
  $sth->execute (time);
  while (my @row = $sth->fetchrow_array)
    {
      print join ("\t", @row), "\n";
    }
}

main (@ARGV);

# eof
