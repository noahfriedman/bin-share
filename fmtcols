#! /bin/sh
exec ${PERL-perl} -wSx $0 ${1+"$@"}
#!perl

# fmtcols --- indent columns so they line up

# Copyright (C) 1997, 2000 Noah S. Friedman

# Author: Noah Friedman <friedman@splode.com>
# Created: 1997-08-02

# $Id: fmtcols,v 2.4 2000/07/22 10:28:35 friedman Exp $

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, you can either send email to this
# program's maintainer or write to: The Free Software Foundation,
# Inc.; 59 Temple Place, Suite 330; Boston, MA 02111-1307, USA.

# Commentary:
# Code:

use Getopt::Long;
use Symbol;
use strict;

my (@line, @maxwidth, %right_justify, $fieldsep, $width_limit);

sub min { return [ sort {   $a <=> $b  } @_ ]->[0]; }
sub max { return [ sort { -($a <=> $b) } @_ ]->[0]; }

# This function reads blocks of data into a buffer and then calls an
# anonymous subroutine with a reference to the data.  That subroutine
# should return the offset of any unexamined data remaining so that it can
# be prepended to the next block of read data; in this way, data which
# fails to match a regexp because it was truncated should be successfully
# matched next time.  (It also may return -1 to indicate discarding all
# existing data.)
#
# Surprisingly, doing repeated global regexp matching on a buffer instead
# of reading line-at-a-time, eliminates an extra copy and tends to make
# scanning a little bit faster than using line-at-a-time with <$fh>.
sub map_over_file_contents ($@)
{
  my $fn = shift;
  my $fh = gensym;
  my $blocksize = 10 * 1024 * 1024; # 10mb

  foreach my $file (@_)
    {
      if ($file eq "-")
        {
          open ($fh, "<&STDIN");
        }
      elsif (!sysopen ($fh, $file, 0))
        {
          die join (": ", $file, "$!");
        }

      my $bytesread = $blocksize;
      my $offset = 0;
      $_ = "";
      while (defined $bytesread && $bytesread == $blocksize)
        {
          my $len = length;
          $_ = substr ($_, $offset) if ($offset > 0 && $offset < $len);
          $_ = "" if ($offset < 0 || $offset >= $len);
          $bytesread = sysread ($fh, $_, $blocksize, length);
          $offset = &$fn (\$_);
        }
      close ($fh);
    }
}

sub read_input
{
  my $fieldsep = shift;

  map_over_file_contents
    sub {
      while (${$_[0]} =~ /(.*)\n/gcmo)
        {
          my @fields = split (/$fieldsep/o, $1, -1);
          my $i = 0;

          push @line, \@fields;
          foreach my $f (@fields)
            {
              my $l = length $f;
              $maxwidth[$i] = $l
                if (!defined $maxwidth[$i] || $l > $maxwidth[$i]);
              $i++;
            }
        }
      return pos ${$_[0]};
    }, @ARGV;
}

sub print_output
{
  my ($fieldsep, $width_limit) = @_;

  my @fmts;
  my $i = 0;
  foreach my $width (@maxwidth)
    {
      my $w = (defined $width_limit ? min ($width, $width_limit) : $width);
      push @fmts, join ("",
                        (exists $right_justify{$i} ? "%" : "%-"),
                        $w,
                        (defined $width_limit ? "." . ($w-1) : ""),
                        "s");
      $i++;
    }
  $fmts[$#fmts] = "%s" if (substr ($fmts[$#fmts], 1, 1) eq '-');
  my $fmtstr = join (" ", @fmts);

  foreach my $l (@line)
    {
      my $s = sprintf ($fmtstr, @$l);
      $s =~ s/\s+$//o;
      print $s, "\n";
    }
}

sub parse_options
{
  $fieldsep = "[ \t]+";
  $width_limit = undef;
  undef %right_justify;
  my @rightcols;

  Getopt::Long::config ('bundling', 'autoabbrev');
  GetOptions ("s|separator=s", \$fieldsep,
              "m|max-field-width=i", \$width_limit,
              "r|right-justify=s@", \@rightcols,
              "h|help", \&usage);

  foreach my $col (split (/\s*,\s*/o, join (",", @rightcols)))
    {
      $right_justify{$col} = 1;
    }
}

sub usage ()
{
  $0 =~ s|.*/||;
  print "Usage: $0 {options} [files {...}]\n
Options are:
-h, --help                      You're looking at it.
-r, --right-justify  F0,F1,...  Right-justify fields F0, F1, ...
-s, --separator      SEP        Field separator between columns.
                                This can be any regular expression.
                                The default field separator is any number of
                                tabs and spaces, i.e. \"[ \\t]+\".
-m, --max-field-width MAX       Truncate fields on output that are larger than
                                this limit.  By default, there is no limit.\n";
  exit (1);
}

sub main
{
  parse_options ();
  push @ARGV, "-" unless (scalar @ARGV > 0);
  read_input ($fieldsep);
  print_output ($fieldsep, $width_limit);
}

main ();

# local variables:
# mode: perl
# eval: (auto-fill-mode 1)
# end:

# fmtcols ends here
